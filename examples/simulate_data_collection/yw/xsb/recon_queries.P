
% RQ0: What URI variable values are associated with resource run/data/DRT322/DRT322_11000eV_028.img?
:- table rq0/2.
rq0(VarName, VarValue) :-
    resource(R, 'run/data/DRT322/DRT322_11000eV_028.img'),
    resource_metadata(R, VarName, VarValue, _).

% RQ1: What samples did the run of the script collect images from?
:- table rq1/1.
rq1(Sample) :-
    data(D, _, 'simulate_data_collection[raw_image]'),
    data_resource(D, R),
    written_resource_metadata(R, 'sample_id', Sample).

% RQ2: What energies were used during collection of images from sample DRT322? 
:- table rq2/1.
rq2(Energy) :-
    data(D, _, 'simulate_data_collection[raw_image]'),
    data_resource(D, R),
    written_resource_metadata(R, 'sample_id', 'DRT322'),
    written_resource_metadata(R, 'energy', Energy).

% RQ3: Where is the raw image from which corrected image run/data/DRT322/DRT322_11000eV_028.img is derived?
:- table rq3/1.
rq3(RawImageFile) :-
    resource(R1, 'run/data/DRT322/DRT322_11000eV_028.img'),
    resource(R2, RawImageFile),
    depends_on(R1, R2).
        
% RQ4: Are there any raw images for which there are no corresponding corrected images?
:- table rq4/1.
rq4(RawImageFile) :-
    data(D, _, 'simulate_data_collection[raw_image]'),
    data_resource(D, R),
    not raw_has_corrected_image(R),
    resource(R, RawImageFile).    
:- table raw_has_corrected_image/1.
raw_has_corrected_image(RawImageResource) :- 
    data_resource(D, RawImageResource),
    data(D, _, 'simulate_data_collection[raw_image]'),
    depends_on(CorrectedImageResource, RawImageResource),
    data_resource(CorrectedImageData, CorrectedImageResource),
    data(CorrectedImageData, _, 'simulate_data_collection[corrected_image]').

% RQ5: What cassette held the sample from which run/data/DRT240/DRT240_10000eV_010.img was derived?
:- table rq5/1.
rq5(Cassette) :-
   resource(R1, 'run/data/DRT240/DRT240_10000eV_010.img'),
   resource_metadata(R2, 'cassette_id', Cassette, _),
   depends_on(R1, R2).
